/* $ java java_cup.Main < ycalc.cup */
/* ----------------------Preliminary Declarations Section--------------------*/
/* Import the class java_cup.runtime.*  */
import java_cup.runtime.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuilder called 'm' with the string 'Error' in it. */
        StringBuilder m = new StringBuilder("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuilder error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuilder error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuilder error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuilder 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};
   

   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  

   Terminals that have no value are listed first and then terminals
   that do have an value, in this case an integer value, are listed on
   the next line down. */
terminal                PLUS, MINUS, TIMES, DIVIDE, LPAREN, RPAREN;
terminal Double        NUMBER;
   
/* Non terminals used in the grammar section.  

   Non terminals that have an object value are listed first and then
   non terminals that have an integer value are listed.  An object
   value means that it can be any type, it isn't set to a specific
   type.  So it could be an Integer or a String or whatever. */
non terminal            program;
non terminal Token      infix, prefix, postfix;
non terminal Token      infix_expr, infix_factor, infix_term;
   

/* -------------Precedence and Associatively of Terminals Section----------- */
   
/*
  Precedence of non terminals could be defined here.  If you do define
  precedence here you won't need to worry about precedence in the
  Grammar Section, i.e. that TIMES should have a higher precedence
  than PLUS.
  
  The precedence defined here would look something like this where the
  lower line always will have higher precedence than the line before it.
   
  precedence left PLUS, MINUS; 
  precedence left TIMES, DIVIDE; 
*/


/* ----------------------------Grammar Section-------------------- */

program ::= infix:i {: System.out.println("= " + i.getValue());
              System.out.println("Prefix: " + i.getPrefExp());
                System.out.println("Postfix: " + i.getPosExp()); :};

infix ::= infix_expr:e {: RESULT = e; :};
   
infix_expr    ::= infix_expr:e PLUS infix_term:t
                {: Token token = new Token();
                   token.setValue(e.getValue() + t.getValue());
                   token.setPrefExp("+ " + e.getPrefExp() + " " + t.getPrefExp());
                   token.setPosExp(e.getPosExp() + " " + t.getPosExp() + " +");
                    RESULT = token; :}
            | infix_expr:e MINUS infix_term:t
                {: Token token = new Token();
                   token.setValue(e.getValue() - t.getValue());
                   token.setPrefExp("- " + e.getPrefExp() + " " + t.getPrefExp());
                   token.setPosExp(e.getPosExp() + " " + t.getPosExp() + " -");
                    RESULT = token; :}
            | infix_term:t
                {: RESULT = t; :};

infix_term    ::= infix_term:t TIMES infix_factor:f
                {: Token token = new Token();
                   token.setValue(t.getValue() * f.getValue());
                   token.setPrefExp("* " + t.getPrefExp() + " " + f.getPrefExp());
                   token.setPosExp(t.getPosExp() + " " + f.getPosExp() + " *");
                    RESULT = token; :}
            | infix_term:t DIVIDE infix_factor:f
                {: Token token = new Token();
                   token.setValue(t.getValue() * f.getValue());
                   token.setPrefExp("/ " + t.getPrefExp() + " " + f.getPrefExp());
                   token.setPosExp(t.getPosExp() + " " + f.getPosExp() + " /");
                    RESULT = token; :}
            | infix_factor:f
                {: RESULT = f; :};

infix_factor    ::= LPAREN infix_expr:e RPAREN
                {: RESULT = e; :}
            | NUMBER:num
                {: Token token = new Token();
                   token.setValue(num);
                   token.setPrefExp(num.toString());
                    token.setPosExp(num.toString());
                    RESULT = token; :};
